#!groovy

def evLibrary = library(identifier: 'evernym-aws-codebuild@aws-codebuild', retriever: modernSCM(
    github(credentialsId: 'evernym-github-machine-user', repoOwner: 'evernym', repository: 'jenkins-shared')
)).com.evernym.pipeline

logger = evLibrary.Logger.new(this)
notifier = evLibrary.Notifier.new(this)
logger.setGlobalLevel('TRACE')

pipelineWrapper({

    nodeWrapper {
        def osname = 'xenial'
        List projects = ['sovtoken', 'sovtokenfees']

        stage('Checkout sources from SCM') {
            checkout scm
        }

        def evernymRepo = evLibrary.EvernymRepo.new(this)
        def utils = evLibrary.Utils.new(this)
        def buildCtx = evLibrary.AwsCodeBuildHelper.BuildCtx.new('plugin',
            ['devops', 'sovtoken/__metadata__.py', 'sovtokenfees/__metadata__.py'])
        def awsCBHelper = evLibrary.AwsCodeBuildHelper.new(this, buildCtx)


        stage('Upload source to S3') {
            awsCBHelper.uploadSourceToS3()
        }

        Map builds = projects.collectEntries { project ->
            ["$osname:$project": {

                def packageName = project
                def prTag = "ci-$osname-$project" // use ci project
                def srcVersion
                def lastRevision
                def debPVersion
                def goals = ['package']

                def ciImageTag

                stage('Get source version') {
                    srcVersion = utils.shStdout("SRC_DIR_NAME=$project make -C devops src_version -s")
                    logger.info("Current source version: $srcVersion")
                }

                stage('Get last revision') {
                    lastRevision = evernymRepo.getLastRevision {
                        delegate.packageName = packageName
                        packageSrcVersion = srcVersion
                        repoDistr = 'agency-dev'
                    }

                    if (lastRevision) {
                        logger.info("Found last revision number: $lastRevision")
                    } else {
                        logger.info("No previous revision was found")
                    }
                }

                stage('Set release parameters') {
                    def releaseVersion = "${lastRevision ? lastRevision[0] + 1: 1}.$BUILD_NUMBER"

                    debPVersion = utils.packageVersion('deb', srcVersion, releaseVersion)
                    logger.info("Package version for evernym repo: $debPVersion")
                }

                stage("$osname:$project: Resolve image tag") {
                    def _imgVersion = utils.shStdout("OSNAME=$osname make -C devops image_ci_version -s")
                    ciImageTag = "$_imgVersion-$osname-$project-ci"
                    logger.info("CI docker image tag: $ciImageTag")
                }

                awsCBHelper.build() {
                    projectTag = prTag

                    // build spec for env image
                    envBuildSrc = ['devops'] // TODO make more accurate
                    envBuildCmds = [
                        'export PROJECT_DIR=$PWD',
                        'make -C devops image_ci'
                    ]
                    envBuildEnvv = [
                        [name: 'OSNAME', value: osname],
                        [name: 'DOCKER_NAME', value: awsCBHelper.buildCtx.projectName],
                        [name: 'DOCKER_TAG', value: ciImageTag],
                    ]

                    // env and build spec
                    imageTag = ciImageTag
                    buildspec = 'devops/aws-codebuild/buildspec.yml'
                    envv = [
                        [name: 'OSNAME', value: osname],
                        [name: 'MAKE_GOALS', value: 'package'],
                        [name: 'SRC_DIR_NAME', value: project],
                        [name: 'ARTIFACTS', value: "devops/_build/$project/*$packageName*.*"], // TODO more accurate here
                        [name: 'FPM_P_VERSION', value: debPVersion],
                    ]

                    onArtifacts = {
                        this.stage("$osname:$project: Archive logs") {
                            utils.archiveArtifacts("logs/*.out*") {
                                truncate = true
                                allowEmptyArchive = true
                            }
                        }
                    }
                }

                stage('Upload deb to Evernym repo') {
                    String debName

                    dir("${awsCBHelper.buildCtx.projects[prTag].artifactsDir}") {
                        debName = utils.shStdout("ls $packageName*$debPVersion*.deb")

                        logger.info("Uploading debian package '$debName' to evernym repo")
                        evernymRepo.upload {
                            repoDistr = 'agency_dev'
                            packagePath = debName
                        }
                    }

                    notifier.email {
                        to = '$DEFAULT_RECIPIENTS ' + (this.env.LIBSOVTOKEN_DEF_RECIPIENTS ?: '')
                        subject = '$PROJECT_NAME - Build # $BUILD_NUMBER: ' + "new deb '$debName' was published"
                        body = ("New debian package '$debName' was built and published" +
                            '\n\nCheck console output at $BUILD_URL to view the results.')
                    }
                }

            }]
        }

        stage("Package and publish") {
            builds.failFast = false
            utils.parallel builds
        }
    }
}, { err ->
    if (err) {
        logger.error("Pipeline failed with the error $err")
    }

    stage("Build result notification") {
        notifier.email {
            to = '$DEFAULT_RECIPIENTS ' + (this.env.LIBSOVTOKEN_DEF_RECIPIENTS ?: '')
        }
    }
})
